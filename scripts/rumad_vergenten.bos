#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, body, pelvis, lfoot, rfoot, rthigh, lthigh, 
		rgun, rfire,
		lgun, lfire,
		turret, barrel1, barrel2, flare1, flare2;

static-var  bMoving, Shot, bHeading;

// Signal definitions
#define SIG_AIM_1				2
#define SIG_AIM_2				4
#define SIG_AIM_3				8


MotionControl(moving, aiming, justmoved)
{
	justmoved = TRUE;
	while( TRUE )
	{
		if( bMoving )
		{
				if( bMoving )
				{
					turn lthigh to x-axis <15.> speed <200.>;
					turn lfoot to x-axis <30.> speed <200.>;
					turn rthigh to x-axis <-75.> speed <500.>;
					turn rfoot to x-axis <45.> speed <300.>;
					move pelvis to y-axis [2.] speed [16.];
					sleep 125;
				}
				if( bMoving )
				{
					turn lthigh to x-axis <45.> speed <200.>;
					turn lfoot to x-axis <15.> speed <100.>;
					turn rthigh to x-axis <-90.> speed <100.>;
					turn rfoot to x-axis <90.> speed <300.>;
					move pelvis to y-axis [0.] speed [16.];
					sleep 125;
				}
				if( bMoving )
				{
					turn lthigh to x-axis <15.> speed <200.>;
					turn lfoot to x-axis <-30.> speed <300.>;
					turn rthigh to x-axis <-15.> speed <450.>;
					turn rfoot to x-axis <30.> speed <400.>;
					move pelvis to y-axis [2.] speed [16.];
					sleep 125;
				}
				if( bMoving )
				{
					turn lthigh to x-axis <-75.> speed <500.>;
					turn lfoot to x-axis <45.> speed <450.>;
					turn rthigh to x-axis <15.> speed <200.>;
					turn rfoot to x-axis <30.> speed <200.>;
					move pelvis to y-axis [2.] speed [16.];
					sleep 125;
				}
				if( bMoving )
				{
					turn lthigh to x-axis <-90.> speed <100.>;
					turn lfoot to x-axis <90.> speed <300.>;
					turn rthigh to x-axis <45.> speed <200.>;
					turn rfoot to x-axis <15.> speed <100.>;
					move pelvis to y-axis [0.] speed [16.];
					sleep 125;
				}
				if( bMoving )
				{
					turn lthigh to x-axis <-15.> speed <450.>;
					turn lfoot to x-axis <30.> speed <400.>;
					turn rthigh to x-axis <15.> speed <200.>;
					turn rfoot to x-axis <-30.> speed <300.>;
					move pelvis to y-axis [2.] speed [16.];
					sleep 125;
				}
			
		justmoved = TRUE;
		}
		if( !bMoving )
		{
			if( justmoved )
			{
				turn lthigh to x-axis <0.> speed <150.>;
				turn lfoot to x-axis <0.> speed <150.>;
				turn rthigh to x-axis <0.> speed <150.>;
				turn rfoot to x-axis <0.> speed <150.>;
				move pelvis to y-axis [0.] speed [8.];
				justmoved = FALSE;
			}
			sleep 100;
		}
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	
	start-script MotionControl();
	
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from body;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	bMoving = FALSE;
	bHeading = 0;
	Shot = 0;
	start-script SmokeUnit();
}

RestoreAfterDelay()
{
	sleep 1500;
	turn turret to y-axis <0> speed <75.000000>;
	turn turret to x-axis <0> speed <50.000000>;
}

RestoreBody()
{
	sleep 500;
	bHeading = 0;
	turn body to y-axis <0> speed <75.000000>;
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
}

AimPrimary(heading, pitch)
{	
	signal SIG_AIM_1;
	set-signal-mask SIG_AIM_1;
	turn turret to y-axis heading - bHeading speed <75.000000>;
	turn turret to x-axis <0> - pitch speed <50.0>;
	wait-for-turn turret around y-axis;
	wait-for-turn turret around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

QueryPrimary(piecenum)
{
	piecenum = flare1;
}

FirePrimary()
{
	emit-sfx 1024 from flare1;
	move barrel1 to z-axis [-1.500000] speed [500.000000];
	sleep 100;
	move barrel1 to z-axis [0.000000] speed [1.000000];
}

AimFromSecondary(piecenum)
{
	piecenum = turret;
}

AimSecondary(heading, pitch)
{	
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	wait-for-turn turret around y-axis;
	wait-for-turn turret around x-axis;
	return (1);
}

QuerySecondary(piecenum)
{
	piecenum = flare2;
}

FireSecondary()
{
	emit-sfx 1024 from flare2;
	move barrel2 to z-axis [-1.500000] speed [500.000000];
	sleep 100;
	move barrel2 to z-axis [0.000000] speed [1.000000];
}

AimFromTertiary(piecenum)
{
	piecenum = body;
}

AimTertiary(heading, pitch)
{	
	signal SIG_AIM_3;
	set-signal-mask SIG_AIM_3;
	if( Shot > 1 )
	{
		start-script RestoreBody();
		return (0);
	}
	bHeading =  heading;	
	turn body to y-axis heading speed <75.000000>;
	wait-for-turn body around y-axis;
	start-script RestoreBody();
	return (1);
}

QueryTertiary(piecenum)
{
	if (Shot == 1) 
	{
		hide rgun;
		piecenum = rfire;
	}
	
	if (Shot == 2) 
	{	
		hide lgun;
		piecenum = lfire;
	}
}

FireTertiary()
{
	++Shot;	
	sleep 25;
	if (Shot == 1) 
	{
		hide rgun;
	}
	
	if (Shot == 2)
	{	
		hide lgun;
	}

}

StartMoving()
{
	bMoving = TRUE;
}

StopMoving()
{
	bMoving = FALSE;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode lthigh type NOHEATCLOUD;
		explode rthigh type NOHEATCLOUD;
		explode rfoot type NOHEATCLOUD;
		explode lfoot type NOHEATCLOUD;
		explode turret type NOHEATCLOUD;
		explode body type NOHEATCLOUD;

		return (corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode lthigh type NOHEATCLOUD;
		explode rthigh type EXPLODE_ON_HIT | NOHEATCLOUD;
		explode rfoot type FALL | EXPLODE_ON_HIT | NOHEATCLOUD;
		explode lfoot type FALL | EXPLODE_ON_HIT | NOHEATCLOUD;
		explode turret type NOHEATCLOUD;
		explode body type NOHEATCLOUD;

		return (corpsetype);
	}
	corpsetype = 3;	
	explode lthigh type SHATTER | EXPLODE_ON_HIT | NOHEATCLOUD;
	explode rthigh type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | NOHEATCLOUD;
	explode rfoot type FALL | EXPLODE_ON_HIT | NOHEATCLOUD;
	explode lfoot type FALL | EXPLODE_ON_HIT | NOHEATCLOUD;
	explode turret type FALL | EXPLODE_ON_HIT | NOHEATCLOUD;
	explode body type FALL | EXPLODE_ON_HIT | NOHEATCLOUD;
	return (corpsetype);
}
