#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, land_01, land_02, land_03, land_04, land_05, 
		body, bridge, engine1, engine2, beam1 , beam2, winglets, frame,
		turret1y, turret1x, gun1a, gun1b, flare1a, flare1b,
		turret2y, turret2x, gun2a, gun2b, flare2a, flare2b,
		blades1, blades2, blades3;
   

static-var  gun_1, gun_2;

// Signal definitions
#define SIG_AIM_1		8
#define SIG_AIM_2		16

Jammer()
{
	while(TRUE)
	{
		turn blades1 to y-axis rand(<-45>,<45>) speed <45>;
		turn blades2 to y-axis rand(<-45>,<45>) speed <45>;
		turn blades3 to y-axis rand(<-45>,<45>) speed <45>;
		sleep 1000;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	
	start-script Jammer();
	
	
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	gun_1 = 0;
	hide flare1a;
	hide flare1b;
	hide flare2a;
	hide flare2b;
	turn turret2y to y-axis <180.000000> now;
	start-script SmokeUnit();
}

QueryLandingPad(Func_Var_1, Func_Var_2, Func_Var_3, Func_Var_4, Func_Var_5, Func_Var_6)
{
	Func_Var_1 = 1;
	Func_Var_2 = 2;
	Func_Var_3 = 3;
	Func_Var_4 = 4;
	Func_Var_5 = 5;
	Func_Var_6 = 6;
}

RestoreAfterDelay1()
{
	sleep 5000;
	turn turret1y to y-axis <0.000000> speed <150.000000>;
	turn turret1x to x-axis <0.000000> speed <150.000000>;
}

RestoreAfterDelay2()
{
	sleep 5000;
	turn turret2y to y-axis <180.000000> speed <150.000000>;
	turn turret2x to x-axis <0.000000> speed <150.000000>;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM_1;
	set-signal-mask SIG_AIM_1;
	turn turret1y to y-axis heading speed <450.000000>;
	turn turret1x to x-axis <0.000000> - pitch speed <450.000000>;
	wait-for-turn turret1y around y-axis;
	wait-for-turn turret1x around x-axis;
	start-script RestoreAfterDelay1();
	return (1);
}

FireWeapon1()
{
	++gun_1;
	if( gun_1 == 2 )
	{
		gun_1 = 0;
	}
}

AimFromWeapon1(piecenum)
{
	piecenum = turret1y;
}

QueryWeapon1(piecenum)
{
	if( gun_1 == 0 )
	{
		piecenum = flare1a;
	}
	if( gun_1 == 1 )
	{
		piecenum = flare1b;
	}
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	turn turret2y to y-axis heading speed <450.000000>;
	turn turret2x to x-axis <0.000000> - pitch speed <450.000000>;
	wait-for-turn turret2y around y-axis;
	wait-for-turn turret2x around x-axis;
	start-script RestoreAfterDelay2();
	return (1);
}

FireWeapon2()
{
	++gun_2;
	if( gun_2 == 2 )
	{
		gun_2 = 0;
	}
}

AimFromWeapon2(piecenum)
{
	piecenum = turret2y;
}

QueryWeapon2(piecenum)
{
	if( gun_2 == 0 )
	{
		piecenum = flare2a;
	}
	if( gun_2 == 1 )
	{
		piecenum = flare2b;
	}
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode body type FALL | SMOKE | FIRE | NOHEATCLOUD;
		return (corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode body type FALL | SMOKE | FIRE | NOHEATCLOUD;
		return (corpsetype);
	}
	corpsetype = 3;
	explode body type FALL | SMOKE | FIRE | NOHEATCLOUD;
	return (corpsetype);
}
