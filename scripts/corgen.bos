#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, body, towers, holo, lights, metal, emit;

static-var  mmState, Active, Effects;


SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Go()
{
	Effects = 1;
	cache lights;
	hide lights;
	show metal;
	dont-cache metal;
}

Stop()
{
	Effects = 0;
	cache metal;
	hide metal;
	show lights;
	dont-cache lights;
}

MMStatus(State)
{
	Active = State;
	//get PRINT(Active);
}

ImAMetalMaker()
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 150;
	}
	mmState = -1;
	while( TRUE )
	{	
		if (Active == 1)
		{
			if (mmState != 1) {
				call-script Go();
				mmState = 1;
			}
		}
		else
		{
			if (mmState != 0) {
				call-script Stop();
				mmState = 0;
			}
		}
		sleep 1000;
	}
}

EffectWatch()
{
	while(TRUE)
	{
		while( Effects  == 1)
		{
			emit-sfx 1024 + 0 from emit;
			sleep 1600;
		}
	sleep 333;
	}
}

Create()
{
    Effects = 0;
	dont-shade metal;
	dont-cache lights;
	start-script SmokeUnit();
	start-script ImAMetalMaker();
	start-script EffectWatch();
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode base type BITMAPONLY | BITMAP1;
		explode body type SHATTER | EXPLODE_ON_HIT | BITMAP2;
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode base type BITMAPONLY | BITMAP1;
		explode body type SHATTER | EXPLODE_ON_HIT | BITMAP2;
		return (0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3;
		explode base type BITMAPONLY | BITMAP1;
		explode body type SHATTER | EXPLODE_ON_HIT | BITMAP2;
		return (0);
	}
	corpsetype = 3;
	explode base type BITMAPONLY | BITMAP1;
	explode body type SHATTER | EXPLODE_ON_HIT | BITMAP2;
}
